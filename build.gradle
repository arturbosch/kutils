plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'org.gradle.maven-publish'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'io.gitlab.arturbosch.detekt' version '1.7.4'
    id 'org.sonarqube' version '2.8'
}

group 'io.gitlab.arturbosch'
version kutilsVersion

repositories {
    mavenCentral()
    jcenter {
        content {
            includeGroup "org.jetbrains.kotlinx"
        }
    }
    maven { url = uri("https://oss.jfrog.org/artifactory/oss-snapshot-local") }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}

detekt {
    failFast = true
    buildUponDefaultConfig = true
    autoCorrect = true
    config = files("$projectDir/detekt/config.yml")
    baseline = file("$projectDir/detekt/baseline.xml")

    reports {
        txt.enabled = true
        html.enabled = false
        xml.enabled = false
    }
}

task detektProjectBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(
            "$rootDir/src/main",
            "$rootDir/src/test"
    ))
    config.setFrom(files("$rootDir/detekt/config.yml"))
    baseline.set(file("$rootDir/detekt/baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
}

dependencies {
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintestVersion"
    testImplementation "org.slf4j:slf4j-nop:$slf4jVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitEngineVersion"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: ""
    key = System.getenv("BINTRAY_API_KEY") ?: ""
    publications = ["kutils"]

    pkg {
        repo = 'generic'
        name = 'kutils'
        userOrg = 'arturbosch'
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/arturbosch/kutils"

        version {
            name = project.version
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        kutils(MavenPublication) {
            artifact sourcesJar
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
