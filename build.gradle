buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'io.gitlab.arturbosch.detekt' version '1.2.2'
    id 'org.sonarqube' version '2.8'
}

apply plugin: 'maven-publish'
apply plugin: 'kotlin'

apply plugin: 'io.gitlab.arturbosch.detekt'

group 'io.gitlab.arturbosch'
version kutilsVersion

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url = uri("https://oss.jfrog.org/artifactory/oss-snapshot-local") }
}

[compileKotlin, compileTestKotlin].each { task ->
    task.configure {
        kotlinOptions {
            freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental", "-Xjvm-default=enable"]
            jvmTarget = "1.8"
        }
    }
}

detekt {
    failFast = true
    buildUponDefaultConfig = true
    config = files("$projectDir/detekt/config.yml")
    baseline = file("$projectDir/detekt/baseline.xml")

    reports {
        html.enabled = true
    }
}

dependencies {
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
    detekt "io.gitlab.arturbosch.detekt:detekt-core:$detektVersion"
    detekt "io.gitlab.arturbosch.detekt:detekt-api:$detektVersion"
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintestVersion"
    testImplementation "org.slf4j:slf4j-nop:$slf4jVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitEngineVersion"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: ""
    key = System.getenv("BINTRAY_API_KEY") ?: ""
    publications = ["kutils"]

    pkg {
        repo = 'generic'
        name = 'kutils'
        userOrg = 'arturbosch'
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/arturbosch/kutils"

        version {
            name = project.version
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        kutils(MavenPublication) {
            artifact sourcesJar
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
